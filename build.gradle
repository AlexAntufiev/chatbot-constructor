plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'com.moowork.node' version '1.2.0'
    id 'io.freefair.lombok' version '3.1.4'
    id 'checkstyle'
    id 'idea'
}

version '0.1.1'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

checkstyle {
    toolVersion "8.17"
    maxWarnings 0
    maxErrors 0
}

bootJar {
    mainClassName = 'chat.tamtam.bot.Launcher'
    archiveBaseName = 'chatbot-constructor'
    archiveClassifier = new Date().format('yyyyMMddHHmmss')
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-websocket' // SockJS
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery:2.1.1.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-consul-config:2.1.1.RELEASE'
    implementation 'org.springframework.security:spring-security-core:5.1.4.RELEASE'
    implementation 'org.springframework.security:spring-security-messaging:5.1.5.RELEASE'
    implementation 'com.hazelcast:hazelcast:3.12'
    implementation 'com.hazelcast:hazelcast-spring:3.12'
    implementation 'org.springframework.boot:spring-boot-configuration-processor:2.1.4.RELEASE'
    implementation 'com.rometools:rome:1.12.0'

    implementation 'org.jetbrains:annotations:16.0.3'
    implementation 'com.auth0:java-jwt:3.4.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'chat.tamtam:tamtam-bot-api:0.1.3'
    
    runtimeOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2:1.4.199'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

task updateNpmDependences(type: NpmTask) {
    args = ['install']
}

task webpack(type: NpmTask) {
    args = ['run', 'build']
}

webpack.dependsOn 'updateNpmDependences'
processResources.dependsOn 'webpack'

bootRun.dependsOn build